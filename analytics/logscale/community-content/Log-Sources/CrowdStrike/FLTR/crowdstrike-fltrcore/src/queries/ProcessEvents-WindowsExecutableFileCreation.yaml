name: Process Events - Windows Executable File Creation
visualization:
  options: {}
  type: table-view
$schema: https://schemas.humio.com/query/v0.1.0
timeInterval:
  isLive: false
  start: 7d
queryString: |-
  // Process Events - Windows Executable File Creation

  // Initial filter to only deal with the events we need in the remainder of the query.
  // This is optimised to make best use of the tags in the repo.
  #event_simpleName=/(ExecutableWritten$|PeFileWritten$|^SuspiciousEseFileWritten$|^ProcessRollup2$)/

  // Restrict to just the Windows platform.
  | event_platform=Win

  // We rename and/or reassign some fields and values depending on the event type we're looking at.
  | case {
      #event_simpleName=ProcessRollup2
        | ProcessId:=TargetProcessId
        | SHA256ProcessHash:=SHA256HashData ;
      *
        | ProcessId:=ContextProcessId
        | SHA256FileHash:=SHA256HashData ;
    }

  // SelfJoinFilter will filter events so that only those with a processID that matches both where statements are passed through.
  // This can introduce some false positives, but those are filtered out later after the groupby
  | selfJoinFilter(ProcessId, where=[
        {#event_simpleName=ProcessRollup2 ImageFileName=/\\Users\\/i},
        {#event_simpleName!=ProcessRollup2 SHA256FileHash=* TargetFileName=/\\Users\\.*\.exe$/i}
      ], prefilter=True
    )

  // Add the ComputerName.
  | $"crowdstrike/fltr-core:zComputerName"()

  // Shorten the name for display purposes.
  | regex("(?<ParentFileName>[^\\/|\\\\]*)$", field=ImageFileName, strict=false)
  | regex("(?<ChildFileName>[^\\/|\\\\]*)$", field=TargetFileName, strict=false)

  // Add an aditional field in markdown syntax that's clickable and will pivot back to Falcon
  | $"crowdstrike/fltr-core:zUrlFalconPid"()

  // Groupby the aid and processid, which should be a unique combination
  // Collect all the other interesting field values for each group
  | groupBy([aid, ProcessId], function=[count(as=Count), collect([ComputerName, #event_simpleName, ParentFileName, ChildFileName, "Process Explorer", "Graph Explorer"], limit=1000)], limit=max)

  // This should filter any false positives, if the group only has a single entry then only a single event was found etc
  | Count>3